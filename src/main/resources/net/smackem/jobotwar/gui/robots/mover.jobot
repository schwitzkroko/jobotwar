// robot  'mover'

def damage, dx, dy

// save current damage
start:
    DAMAGE -> damage

// check damage - go move if damaged
// if not, increment aim
scan:
    goto move unless DAMAGE = damage
    AIM + 13 -> AIM 

// send radar in direction of aim
// exit to scan if no enemy found
// or shoot at enemy just spotted
// then go see if enemy is still there

spot:
    AIM -> RADAR
    goto scan if RADAR > 0
    0 - RADAR -> SHOT
    goto spot

// pick a random place to go

move:
    50 + RANDOM * 500 -> dx
    50 + RANDOM * 400 -> dy

// travel to new x location
moveX:
    dx - X -> SPEEDX
    goto moveX if abs(dx - X) > 20
    0 -> SPEEDX

// travel to new y location
// then go start scanning again

moveY:
   dy - Y -> SPEEDY
   goto moveY if abs(dy - Y) > 20
   0 -> SPEEDY
   goto start
